import com.vanniktech.maven.publish.AndroidSingleVariantLibrary

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'org.jetbrains.dokka-javadoc'

//Detailed information about advantages of Java toolchain https://developer.android.com/build/jdks#toolchain
java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(libs.versions.javaVersion.get().toInteger())
  }
}

android {
  compileSdkVersion 35
  namespace 'com.glia.widgets'
  defaultConfig {
    minSdkVersion 24
    versionCode widgetsVersionCode
    versionName widgetsVersionName
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    consumerProguardFiles "consumer-rules.pro"
  }

  buildTypes {
    configureEach {
      buildConfigField("String", "GLIA_CORE_SDK_VERSION", "\"$gliaSdkVersion\"")
      buildConfigField("String", "GLIA_WIDGETS_SDK_VERSION", "\"$defaultConfig.versionName\"")
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
    snapshot {
      // Build type used for snapshot testing.
      apply plugin: 'app.cash.paparazzi'
    }
  }

  lint {
    disable 'WrongLayoutName',
      'LayoutFileNameMatchesClass',
      'MatchingViewId',
      'RawDimen',
      'WrongAnnotationOrder',
      'ColorCasing',
      'WrongViewIdFormat',
      'HardcodedText'
  }

  buildFeatures {
    viewBinding true
    buildConfig true
  }

  //After adding `Mockk` dependency to the instrumentation tests it started to fail builds with message that
  // there are 6 files in project with these two Licences. Since all of them were related to Junit, it is safe to merge them.
  packagingOptions {
    resources {
      merges += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
    }
  }

  testOptions {
    unitTests {
      includeAndroidResources true
      unitTests.all {
        jvmArgs '-Xmx4g'
      }
    }

    packaging {
      resources.excludes += ['META-INF/COPYRIGHT']
    }
  }
  sourceSets {
    // Exclude the default test directory ("src/test/java") for all build types.
    // It is necessary to exclude unit tests from snapshot tests.
    test.java.srcDirs = []

    // Add the default test directory for debug and release build types.
    testDebug.java.srcDirs += ['src/test/java']
    testRelease.java.srcDirs += ['src/test/java']

    // Add resource directory for snapshot testing.
    snapshot.res.srcDirs += ['src/testSnapshot/res']
  }
}

group = 'com.glia'

mavenPublishing {
  def isSnapshot = findProperty("snapshot")?.toString() == "yes"
  //Plugin automatically publishes snapshot to the Maven Central when the version name ends with `-SNAPSHOT`
  //To publish snapshot release simply call the publishing command with gradle property `-Psnapshot=yes`
  //for example `./gradlew androidSdk:publishMavenPublicationToMavenLocal -Psnapshot=yes`

  version = isSnapshot ? widgetsVersionName + "-SNAPSHOT" : widgetsVersionName
  coordinates(group, 'android-widgets', version)
  configure(new AndroidSingleVariantLibrary('release', true, false/* enable javadoc generation */))

  pom {
    excludeOptionalDependencies(it)
  }
}

//Excludes the Firebase Messaging from the .aar dependencies to avoid requiring integrators to include them in their applications.
def excludeOptionalDependencies(mavenPom) {
  mavenPom.withXml {
    final pomNode = asNode()
    pomNode.dependencyManagement.dependencies.'*'.findAll() {
      it.artifactId.text() == 'firebase-bom'
    }.each() {
      it.parent().remove(it)
    }
    pomNode.dependencies.'*'.findAll() {
      it.artifactId.text() == 'firebase-messaging'
    }.each() {
      it.parent().remove(it)
    }
  }
}

tasks.register('publishSnapshotPublicationToMavenLocal', GradleBuild) {
  description = 'Publishes the Snapshot publication to Maven Local with -SNAPSHOT suffix'
  group = 'publishing'

  tasks = ['publishMavenPublicationToMavenLocal']
  startParameter.projectProperties = ['snapshot': 'yes']
}

//The snapshot will be automatically available in the Central Portal snapshot repository directly after the task finished.
tasks.register('publishSnapshotPublicationToMavenCentralRepository', GradleBuild) {
  description = 'Publishes the Snapshot publication to Maven Local with -SNAPSHOT suffix'
  group = 'publishing'

  tasks = ['publishMavenPublicationToMavenCentralRepository']
  startParameter.projectProperties = ['snapshot': 'yes']
}

//For this task we need android core sdk to be set up for releasing to maven local repository.
tasks.register("publishCoreSdkToLocalMaven") {
  if (gliaCoreSdkUseDirect) {
    dependsOn gradle.includedBuild("gliaCore").task(":androidSdk:publishSnapshotPublicationToMavenLocal")
  } else {
    println("Cannot publish Core SDK to local maven repository!")
    println("Core SDK is not configured to be used directly.")
  }
}

// For generating Javadoc jar using Dokka
tasks.register('dokkaJavadocJar', Jar.class) {
  dependsOn(dokkaGeneratePublicationJavadoc)
  from(dokkaGeneratePublicationJavadoc)
  archiveClassifier.set("javadoc")
}

// CRITICAL: Ensure the Dokka JAR is included in the published artifacts.
afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        // CRITICAL: Add the generated Javadoc jar to the published artifacts.
        artifact dokkaJavadocJar
      }
    }
  }
}

apply from: "${rootProject.projectDir}/scripts/direct-core.gradle"

dependencies {
  implementation libs.java.appcompat
  implementation libs.java.material
  implementation libs.java.constraintlayout
  implementation libs.lifecycle.process
  implementation libs.media.coil.core
  implementation libs.media.coil.network

  // Used for audio and video permissions by CallActivity
  implementation libs.java.rxandroid
  implementation libs.java.rxjava

  releaseApi "${libs.glia.core.get().module}:$gliaSdkVersion"

  def coreDebugVersion = coreSdkDebugVersion(gliaSdkVersion)
  debugApi ("${libs.glia.core.get().module}:$coreDebugVersion") {
    if (coreDebugVersion.endsWith("-SNAPSHOT")) {
      changing = true
    }
  }

  // includeBuild doesn't work with custom build types, use local maven version instead
  snapshotApi "${libs.glia.core.get().module}:${coreSdkSnapshotVersion(gliaSdkVersion)}"

  def telemetryVersion = telemetrySdkVersion(telemetryLibVersion)
  implementation ("${libs.glia.telemetry.get().module}:$telemetryVersion") {
    if (telemetryVersion.endsWith("-SNAPSHOT")) {
      changing = true
    }
  }

  implementation libs.media.audioswitch
  implementation libs.media.lottie
  implementation libs.data.gson
  implementation libs.java.core.ktx
  implementation libs.media.exif.interface

  implementation platform(libs.firebase.bom)
  implementation libs.firebase.messaging

  testImplementation libs.test.junit
  testImplementation libs.test.mockito.kotlin
  testImplementation libs.test.core.testing
  testImplementation libs.test.robolectric
  testImplementation libs.test.mockk.android
  androidTestImplementation libs.test.mockk.android
  androidTestImplementation libs.test.ext.junit
  androidTestImplementation libs.test.espresso.core
  androidTestImplementation libs.test.rules

  // Test dependencies to Android native libraries
  // To prevent unit tests firing errors like 'Method length in org.json.JSONObject not mocked'
  testImplementation libs.test.json

  lintChecks project(':lint_checker')
  debugImplementation libs.debug.leakcanary
}

configurations.configureEach {
  // Always check remote repo for changing modules (SNAPSHOTs)
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
