import org.jetbrains.dokka.gradle.DokkaTask

buildscript {
  ext {
    kotlin_version = '1.8.22'
  }
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.0.0'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id("io.github.gradle-nexus.publish-plugin") version "1.1.0"
  id("org.jetbrains.kotlin.android") version "$kotlin_version" apply false
  id("org.jetbrains.dokka") version "1.9.20"
  id("org.jlleitschuh.gradle.ktlint") version "11.5.0"
  id("app.cash.paparazzi") version '1.3.1' apply false
}

allprojects {
  repositories {
    google()
    mavenCentral()
    flatDir {
      dirs '../libs'
    }

    // Needed for some automated and manual testing (e.g: acceptance tests)
    mavenLocal()
  }
}

subprojects {
  apply plugin: 'org.jlleitschuh.gradle.ktlint'

  repositories {
    mavenCentral()
  }

  ktlint {
    outputToConsole.set(true)
    debug.set(true)
    android.set(true)
  }
}

subprojects {
  // This is setup of Dokka. It is used for generating API docs web page from in-doc comments
  // See official Dokka docs for more information: https://kotlin.github.io/dokka/
  apply plugin: 'org.jetbrains.dokka'
  tasks.withType(DokkaTask).configureEach {
    String currentYear = Calendar.getInstance().get(Calendar.YEAR)
    String gliaSvgLogoPath = rootProject.file("dokkaAssets/logo-icon.svg")
    // These configurations currently apply only to dokkaHtml and not to dokkaJavadoc.
    // Have not found how to solve this. But at current Dokka version (1.9.20)
    // Javadoc plugin is still in 'early stages' of development.
    String configurationJson = $/{
      "footerMessage": "(c) ${currentYear} Glia Technologies, Inc. All rights reserved.",
      "customAssets": ["${gliaSvgLogoPath}"]
    }/$
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": configurationJson])

    moduleName = "Glia Android Widgets SDK"
    suppressInheritedMembers.set(true)
  }

  // This adds support for @hide keyword inside in-code docs
  // For some reason artifact ID mentioned in docs does not exist, probably a typo. This one has slightly different order of words
  dependencies {
    dokkaPlugin("org.jetbrains.dokka:android-documentation-plugin:1.9.20")
  }
}

tasks.register('clean', Delete) {
  delete rootProject.buildDir
}

apply from: "${rootDir}/scripts/publish-settings.gradle"
apply from: "${rootDir}/scripts/version-updater.gradle"

ext {
//    gliaSdkVersion = 'X.X.X' // this property is now attached dynamically during gradle sync

  appCompatVersion = '1.3.1'
  materialVersion = '1.6.1'
  constraintLayoutVersion = '2.1.4'
  navVersion = '2.3.5'
  lifecycle_process = '2.3.1'
  rxAndroid = '2.1.0'
  rxJava = '2.2.6'
  gson = '2.8.6'
  preferenceVersion = '1.1.1'
  picassoVersion = '2.8'
  lottieVersion = '5.2.0'
  firebaseVersion = '31.0.2'
  audioswitch = '1.1.8'

  leakCanaryVersion = '2.6'
  espressoVersion = '3.5.1'
  junitVersion = '4.13.2'
  testLibraryVersion = '1.1.5'
  androidXTestVersion = '1.1.5'
  mockitoKotlinVersion = '5.2.1'
  mockitoAndroidTestVersion = '5.8.0'
  mockkVersion = '1.13.10'
  archCoreVersion = '2.2.0'
  testRulesVersion = '1.5.0'
  robolectricVersion = '4.11.1'
  lintVersion = '31.3.0'

  //kotlin
  coreKtxVersion = '1.8.0'
}
