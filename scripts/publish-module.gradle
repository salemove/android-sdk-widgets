apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

publishing {
    publications {
        snapshot(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version = PUBLISH_VERSION + '_snapshot'

            setupFilesForPublishing(it)
        }

        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            setupFilesForPublishing(it)
        }
    }
}

def setupFilesForPublishing(publication) {
    afterEvaluate {
        // Two artifacts, the `aar` (or `jar`) and the sources
        if (project.plugins.findPlugin("com.android.library")) {
            publication.from components.release
        } else {
            publication.from components.java
        }
    }

    // Remove optional dependencies from the Widgets SDK
    // to avoid requiring integrators to include them in their applications.
    publication.pom.withXml {
        final pomNode = asNode()
        pomNode.dependencyManagement.dependencies.'*'.findAll() {
            it.artifactId.text() == 'firebase-bom'
        }.each() {
            it.parent().remove(it)
        }
        pomNode.dependencies.'*'.findAll() {
            it.artifactId.text() == 'firebase-messaging'
        }.each() {
            it.parent().remove(it)
        }
    }

    publication.pom {
        name = PUBLISH_ARTIFACT_ID
        description = PUBLISH_MODULE_DESCRIPTION
        url = PUBLISH_ORGANISATION_URL
        licenses {
            license {
                url = 'https://raw.githubusercontent.com/salemove/android-sdk-widgets/master/LICENSE.txt'
            }
        }
        organization {
            name = PUBLISH_ORGANISATION_NAME
            url = PUBLISH_ORGANISATION_URL
        }
        developers {
            developer {
                name = 'Glia Technologies'
                url = PUBLISH_ORGANISATION_URL
            }
        }
        scm {
            url = 'https://github.com/salemove/android-sdk-widgets.git'
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications.release
}