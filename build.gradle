buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath libs.android.gradle.plugin
  }
}

plugins {
  alias libs.plugins.dokka
  alias libs.plugins.dokkaJavadoc
  alias libs.plugins.maven.publish
  alias libs.plugins.kt.lint
  alias libs.plugins.kotlin.android apply false
  alias libs.plugins.kotlin.compose apply false
  alias libs.plugins.paparazzi apply false
}

apply from: "${rootDir}/scripts/version-updater.gradle"

// Setup git hooks
def setupGitHooksScript = file("${rootDir}/scripts/setup-git-hooks.sh")
if (setupGitHooksScript.exists()) {
  try {
    def process = "bash ${setupGitHooksScript.absolutePath}".execute(null, rootDir)
    process.waitFor()
    logger.info(process.inputStream.text)
  } catch (Exception e) {
    logger.warn("Failed to setup git hooks: ${e.message}")
  }
}

allprojects {

  repositories {
    google()
    mavenCentral()

    // Needed for some automated and manual testing (e.g: acceptance tests)
    mavenLocal()

    // Needed for using SNAPSHOT releases
    maven {
      url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
  }

  // Dokka uses the com.fasterxml.jackson.core version that has vulnerability: Stack-based Buffer Overflow [High Severity][https://security.snyk.io/vuln/SNYK-JAVA-COMFASTERXMLJACKSONCORE-10500754]
  // To fix it, we override the com.fasterxml.jackson.core version for all configurations (mainly for Dokka)
  // This solution is applied until fasterxml version is upgraded in Dokka https://github.com/Kotlin/dokka/issues/4162
  configurations.configureEach {
    resolutionStrategy {
      force 'com.fasterxml.jackson.core:jackson-core:2.15.3'
    }
  }
}

subprojects {
  apply plugin: 'org.jlleitschuh.gradle.ktlint'

  repositories {
    mavenCentral()
  }

  ktlint {
    outputToConsole.set(true)
    debug.set(true)
    android.set(true)
  }
}

// This is setup of Dokka. It is used for generating API docs web page from in-doc comments
// See official Dokka docs for more information: https://kotlinlang.org/docs/dokka-gradle.html
// Migration to Dokka Gradle plugin v2: https://kotlinlang.org/docs/dokka-migration.html
dokka {
  moduleName = "Glia Android Widgets SDK"
  dokkaPublications {
    html {
      suppressInheritedMembers = true
      failOnWarning = true
    }
  }
  dokkaSourceSets {
    main {
      sourceRoots.from(file("widgetssdk/src/main"))
      // Exclude internal package
      perPackageOption {
        matchingRegex.set(".*internal.*") // Regex to exclude internal
        suppress.set(true)
      }
      sourceLink {
        localDirectory = file("widgetssdk/src/main/java")
        remoteUrl = new URI("https://github.com/salemove/android-sdk-widgets/blob/master/widgetssdk/src/main/java")
        remoteLineSuffix = "#L"
      }
    }
  }
  String gliaSvgLogoPath = rootProject.file("dokkaAssets/logo-icon.svg")
  pluginsConfiguration {
    html {
      customAssets.from("${gliaSvgLogoPath}")
      footerMessage = "Â© Glia Technologies, Inc. All rights reserved."
    }
  }
  dokkaPublications.html {
    outputDirectory.set(file("widgetssdk/build/dokka/html"))
  }

  // This adds support for @hide keyword inside in-code docs
  // For some reason artifact ID mentioned in docs does not exist, probably a typo. This one has slightly different order of words
  dependencies {
    dokkaPlugin(libs.java.dokka)
  }
}
