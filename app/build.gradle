apply plugin: 'com.android.application'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"
    defaultConfig {
        applicationId "com.glia.exampleapp"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }

    File propsFile = project.rootProject.file('local.properties')
    if (propsFile.exists()) {
        Properties p = new Properties()
        new FileInputStream(propsFile).withCloseable { is -> p.load(is) }
        p.each { name, value -> ext[name] = value }
    } else {
        ext["GLIA_API_KEY_SECRET"] = System.getenv("GLIA_API_KEY_SECRET") as String
        ext["GLIA_API_KEY_ID"] = System.getenv("GLIA_API_KEY_ID") as String
        ext["GLIA_APP_TOKEN"] = System.getenv("GLIA_APP_TOKEN") as String
        ext["GLIA_SITE_ID"] = System.getenv("GLIA_SITE_ID") as String
    }
    buildTypes {
        all {
            resValue("string", "app_token", GLIA_APP_TOKEN ?: "")
            resValue("string", "site_id", GLIA_SITE_ID ?: "")
            resValue("string", "glia_api_key_id", GLIA_API_KEY_ID ?: "")
            resValue("string", "glia_api_key_secret", GLIA_API_KEY_SECRET ?: "")
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
        }
        master {
            initWith debug
            applicationIdSuffix '.master'
            matchingFallbacks = ['debug']
        }
        release {
            initWith debug
            applicationIdSuffix ''
        }
    }
    lintOptions {
        disable "WrongLayoutName"
        disable "LayoutFileNameMatchesClass"
        disable "MatchingViewId"
        disable "RawDimen"
        disable "WrongAnnotationOrder"
        disable "ColorCasing"
        disable "WrongViewIdFormat"
        disable "HardcodedText"
    }
}

ext {
    appCompatVersion = '1.2.0'
    materialVersion = '1.2.1'
    constraintLayoutVersion = '2.0.4'
    navVersion = '2.3.1'
    preferenceVersion = '1.1.1'
    junitVersion = '4.13.1'
    testLibraryVersion = '1.1.2'
    espressoVersion = '3.3.0'
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation project(path: ':widgetssdk')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$testLibraryVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}
