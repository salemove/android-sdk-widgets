apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    defaultConfig {
        applicationId "com.glia.exampleapp"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
    }

    File propsFile = project.rootProject.file('local.properties')
    if (propsFile.exists()) {
        Properties p = new Properties()
        new FileInputStream(propsFile).withCloseable { is -> p.load(is) }
        p.each { name, value -> ext[name] = value }
    } else {
        ext["GLIA_API_KEY_SECRET"] = System.getenv("GLIA_API_KEY_SECRET") as String
        ext["GLIA_API_KEY_ID"] = System.getenv("GLIA_API_KEY_ID") as String
        ext["GLIA_SITE_ID"] = System.getenv("GLIA_SITE_ID") as String
        ext["FIREBASE_PROJECT_ID"] = System.getenv("FIREBASE_PROJECT_ID") as String
        ext["FIREBASE_API_KEY"] = System.getenv("FIREBASE_API_KEY") as String
        ext["FIREBASE_APP_ID"] = System.getenv("FIREBASE_APP_ID") as String
        ext["FIREBASE_APP_ID_DEBUG"] = System.getenv("FIREBASE_APP_ID_DEBUG") as String
        ext["FIREBASE_APP_ID_MASTER"] = System.getenv("FIREBASE_APP_ID_MASTER") as String
    }
    buildTypes {
        all {
            resValue("string", "site_id", GLIA_SITE_ID ?: "")
            resValue("string", "glia_api_key_id", GLIA_API_KEY_ID ?: "")
            resValue("string", "glia_api_key_secret", GLIA_API_KEY_SECRET ?: "")
            resValue("string", "firebase_proj_id", FIREBASE_PROJECT_ID ?: "")
            resValue("string", "firebase_api_key", FIREBASE_API_KEY ?: "")
        }
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix '.debug'
            resValue("string", "firebase_app_id", FIREBASE_APP_ID_DEBUG ?: "")
        }
        master {
            initWith debug
            applicationIdSuffix '.master'
            matchingFallbacks = ['debug']
            resValue("string", "firebase_app_id", FIREBASE_APP_ID_MASTER ?: "")
        }
        release {
            initWith debug
            applicationIdSuffix ''
            resValue("string", "firebase_app_id", FIREBASE_APP_ID ?: "")
        }
    }
    lint {
        disable 'WrongLayoutName',
                'LayoutFileNameMatchesClass',
                'MatchingViewId',
                'RawDimen',
                'WrongAnnotationOrder',
                'ColorCasing',
                'WrongViewIdFormat',
                'HardcodedText'
    }
    namespace 'com.glia.exampleapp'
}

dependencies {
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.navigation:navigation-fragment:$navVersion"
    implementation "androidx.navigation:navigation-ui:$navVersion"
    implementation "androidx.preference:preference:$preferenceVersion"
    implementation platform("com.google.firebase:firebase-bom:$firebaseVersion")
    implementation "com.google.firebase:firebase-messaging"
    implementation project(path: ':widgetssdk')
    implementation "androidx.core:core-ktx:$coreKtxVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$testLibraryVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}
